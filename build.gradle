plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '3.0.2'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'checkstyle'
}


group = 'de.unimarburg.diz'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = "17"

repositories {
    mavenCentral()
    maven { url 'https://repo.spring.io/snapshot' }
    maven { url 'https://repo.spring.io/milestone' }
}

ext {
    set('springBootVersion', "3.0.2")
    set('springCloudVersion', "2022.0.0")
    set("hapiFhirVersion", "5.7.9")
}

dependencies {
    // spring cloud stream kafka
    implementation 'org.springframework.cloud:spring-cloud-stream'
    implementation 'org.springframework.cloud:spring-cloud-stream-binder-kafka-streams'
    implementation "org.springframework.kafka:spring-kafka"

    // metrics
    implementation "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion"
    implementation 'io.micrometer:micrometer-registry-prometheus:1.10.4'

    // hapi fhir
    implementation "ca.uhn.hapi.fhir:hapi-fhir-base:$hapiFhirVersion"
    constraints {
        implementation('org.apache.commons:commons-text:1.10.0') {
            because 'CVE-2022-42889'
        }
    }
    implementation "ca.uhn.hapi.fhir:hapi-fhir-client:$hapiFhirVersion"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-structures-r4:$hapiFhirVersion"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-validation:$hapiFhirVersion"
    implementation "ca.uhn.hapi.fhir:hapi-fhir-validation-resources-r4:$hapiFhirVersion"

    // mapper
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-csv'

    // unit tests
    testImplementation 'org.springframework.cloud:spring-cloud-stream'
    testImplementation 'org.apache.kafka:kafka-streams-test-utils'

    testImplementation("org.springframework.boot:spring-boot-starter-test:$springBootVersion") {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    testImplementation 'org.assertj:assertj-core'

}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
    dependsOn test
}


jar {
    enabled = false
}

bootJar {
    mainClass.set('de.unimarburg.diz.labtofhir.LabToFhirApplication')
}
